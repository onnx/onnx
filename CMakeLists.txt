# Minimum CMake required
cmake_minimum_required(VERSION 3.1)

# Project
project(onnxir C CXX)

# Set C++11 as standard for the whole project
set(CMAKE_CXX_STANDARD 11)

# External dependencies
set(CMAKE_MODULE_PATH ${PROJECT_SOURCE_DIR}/external)
set(ONNXIR_ROOT ${PROJECT_SOURCE_DIR}/..)
if (WIN32)
    # parallel build
    add_compile_options(/MP)
    add_compile_options(/WX)
    set(CMAKE_EXE_LINKER_FLAGS "${CMAKE_EXE_LINKER_FLAGS} /WX")
    set(CMAKE_STATIC_LINKER_FLAGS "${CMAKE_STATIC_LINKER_FLAGS} /WX")
endif()
add_subdirectory(${CMAKE_CURRENT_SOURCE_DIR}/../third_party/pybind11 ${CMAKE_CURRENT_BINARY_DIR}/pybind11)
FIND_PACKAGE(Protobuf REQUIRED)
include_directories(
    ${ONNXIR_ROOT}
    ${CMAKE_CURRENT_BINARY_DIR}
    # External dependencies.
    ${PROTOBUF_INCLUDE_DIRS}
    ${PYBIND11_INCLUDE_DIR}
    ${PYTHON_INCLUDE_DIRS}
)

PROTOBUF_GENERATE_CPP(PROTO_SRCS PROTO_HDRS
    ${ONNXIR_ROOT}/onnx/onnx.proto
    ${ONNXIR_ROOT}/onnx/onnx-operators.proto
)

file(GLOB_RECURSE onnx_src
    "${ONNXIR_ROOT}/onnx/*.h"
    "${ONNXIR_ROOT}/onnx/*.cc"
)

file(GLOB_RECURSE onnx_exclude_src
    "${ONNXIR_ROOT}/onnx/onnx.pb.h"
    "${ONNXIR_ROOT}/onnx/onnx.pb.cc"
    "${ONNXIR_ROOT}/onnx/onnx-operators.pb.h"
    "${ONNXIR_ROOT}/onnx/onnx-operators.pb.cc"
)

# Temp solution to remove files generated by calling 'pip install ..."
# Should be removed after changing setup.py to use cmake to build c++ part.
list(LENGTH onnx_exclude_src list_len)
if (list_len GREATER 1)
    list(REMOVE_ITEM onnx_src ${onnx_exclude_src})
endif()

add_library(onnxir ${PROTO_SRCS} ${PROTO_HDRS} ${onnx_src})

if (WIN32)
    target_compile_options(onnxir PRIVATE
        /wd4800 # disable warning type' : forcing value to bool 'true' or 'false' (performance warning)
        /wd4503 # identifier' : decorated name length exceeded, name was truncated
    )
    set(onnx_static_library_flags
        -IGNORE:4221 # LNK4221: This object file does not define any previously undefined public symbols, so it will not be used by any link operation that consumes this library
    )
    set_target_properties(onnxir PROPERTIES
        STATIC_LIBRARY_FLAGS "${onnx_static_library_flags}")
endif()